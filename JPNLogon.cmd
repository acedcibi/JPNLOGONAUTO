REM IJPN System Logon Scripts Version 2
REM Last Update: 17/05/2018 BY RASYID INFOMINA
REM ====================================================================
REM   CHANGES 17/05/2018: 
REM   1. CHECK WINDOWS OS VERSION 
REM   2. DO AUTO SILIN FAIL FOR WINDOWS 7 WORKSTATION
REM   3. CALL SALIN FAIL PROGRAM TO MANUALLY SALIN FAIL FOR WINDOWS 2000
REM ====================================================================

NET TIME /DOMAIN:%USERDOMAIN% /SET /Y

IF %PCID%==002 GOTO END

REM TO CHECK VERSION MENU.INI ON BRANCH SERVER
TYPE \\%USERDOMAIN%001\JPNAPPL\INI\MENU.INI | FIND "Version=" > D:\JPNAPPL\CFG\IJPNSVVER

REM TO CHECK VERSION MENU.INI ON WORKSTATION
TYPE D:\JPNAPPL\INI\MENU.INI | FIND "Version=" > D:\JPNAPPL\CFG\IJPNPCVER

REM TO COMPARE THE MENU.INI VERSIONS OF SERVER AND WORKSATATION. SAVE A LOG FILE
FC "D:\JPNAPPL\CFG\IJPNSVVER" "D:\JPNAPPL\CFG\IJPNPCVER" /W > D:\JPNAPPL\CFG\IJPNVERLOGS.TXT

REM IF IJPNSVVER == IJPNPCVER; ERRORLEVEL 0 THEN GOTO TO VERSION0 = USER LOGIN TO IJPN SYSTEM NORMALLY
REM IF IJPNSVVER != IJPNPCVER; ERRORLEVEL 1 THEN GOTO TO VERSION1 = CHCEK OS VERSION AND DO SALIN FAIL

GOTO VERSION%ERRORLEVEL%

:VERSION0
REM USERLOGIN IJPN
D:
CD \JPNAPPL\BINV6

prgChkUsrIDStatus

D:
CD \JPNAPPL\MYKAD

GOTO END

:VERSION1
REM TO CHECK IF WORKSATATION IS WINDOWS 2000 VERSION

REM GET OS VERSION AND SAVE TO FILE
VER > D:\JPNAPPL\CFG\OSVERSION.TXT

REM CHECK INSIDE FILE FOR ANY WORD OF 2000 
FINDSTR "2000" D:\JPNAPPL\CFG\OSVERSION.TXT > NUL: 2>&1

REM IF ERRORLEVEL 0 THEN GOTO WINSTATUS0 = WINDOWS 2000
REM IF ERRORLEVEL 1 THEN GOTO WINSTATUS1 = WINDOWS 7

GOTO WINSTATUS%ERRORLEVEL%

:WINSTATUS0
REM WINDOWS 2000 - POPUP TETINGKAP PEMBERITAHUAN SALIN FAIL SECARA MANUAL DAN MULAKAN PROGRAM SALINFAIL.EXE

D: 
CD \JPNAPPL\BIN

START CMD /C "MODE 60,15 &TITLE PEMBERITAHUAN WINDOWS 2000 &COLOR 1E &ECHO.&ECHO.&ECHO.&ECHO.  Salin fail secara MANUAL perlu dilakukan &ECHO.  bagi workstation WINDOWS 2000. &ECHO.&ECHO.  Sila hubungi pihak HelpDesk untuk bantuan &ECHO.&ECHO.  atau; &ECHO.&ECHO.  Tekan butang [ENTER] untuk Salin Fail secara manual. &ECHO.&pause>NUL &START SALINFAIL.EXE"

GOTO END


:WINSTATUS1
REM WINDOWS 7 - POPUP TETINGKAP PEMBERITAHUAN DAN SALIN FAIL AKAN DILAKUKAN

D: 
CD \JPNAPPL\BIN

START CMD /C "MODE 60,20 &TITLE SALIN FAIL SEDANG DILAKUKAN. SILA TUNGGU ... &COLOR 3E &ECHO.&ECHO. &CpyAppFiles.cmd"

ECHO. delay start next line of program
ECHO.
ECHO.
ECHO.
ECHO.
ECHO.
ECHO.

START CMD /C "MODE 50,10 &TITLE PEMBERITAHUAN &COLOR 1E &ECHO.&ECHO.&ECHO. Versi Sistem IJPN di Workstation ini tidak sama &ECHO. dengan Versi Sistem IJPN di Server. &ECHO. Salin fail sedang dilakukan.&ECHO.&ECHO. Sila tunggu sehingga selesai... &pause>NUL"

GOTO END

:END
EXIT
